import com.android.build.api.dsl.ManagedVirtualDevice

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

apply from: "$rootDir/gradle/dependencies.gradle"

android {
    namespace "pl.codesamurai.latte.sample"
    compileSdkVersion Integer.parseInt(COMPILE_SDK)

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "pl.codesamurai.latte.sample"
        minSdkVersion Integer.parseInt(MIN_SDK)
        targetSdkVersion Integer.parseInt(TARGET_SDK)
        versionName libVersion
        versionCode libVersionCode
        archivesBaseName = "latte-sample-v$versionName"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets.all { sourceSet ->
        sourceSet.java.srcDirs += "src/${sourceSet.getName()}/kotlin"
    }

    lintOptions {
        abortOnError true
        warningsAsErrors true
        textReport false
        htmlReport false
        xmlOutput file("$rootDir/build/reports/lint/${project.name}.xml")
        lintConfig file('lint.xml')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        allWarningsAsErrors = true
        jvmTarget = "17"
    }

    testOptions {
        animationsDisabled true

        managedDevices {
            devices {
                pixelC(ManagedVirtualDevice) {
                    device = "Pixel 2"
                    apiLevel = 30
                    systemImageSource = "aosp-atd"
                    require64Bit = true
                }
            }
        }
    }
}

dependencies {
    implementation Libs_kotlin

    implementation Libs_androidxAppCompat

    androidTestImplementation project(':lib-core')
    androidTestImplementation project(':lib-core-ktx')

    androidTestImplementation TestLibs_androidxTestExt
    androidTestImplementation TestLibs_androidxRules
    androidTestImplementation TestLibs_androidxRunner
}