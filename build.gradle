buildscript {
    apply from: "$rootDir/gradle/dependencies.gradle"

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath ClassPaths_gradle
        classpath ClassPaths_kotlin
        classpath ClassPaths_dokka
        classpath ClassPaths_benManesVersions
    }
}

apply from: "$rootDir/gradle/dokka-multimodule.gradle"
apply from: "$rootDir/gradle/updates.gradle"

allprojects {
    project.ext {
        libVersion = "0.1.0"
        libVersionCode = generateVersionCode(libVersion)
    }

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
        maven {
            url "https://maven.pkg.github.com/malloth/latte"
            credentials {
                username System.getenv("GITHUB_USER")
                password System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

static def generateVersionCode(String versionName) {
    String[] majorMinorPatch = versionName.split('-').first().split('\\.')

    int major = majorMinorPatch.getAt(0).toInteger()
    if (major > 99) {
        throw new GradleException("Major version of application $major can not be higher than 99")
    }

    int minor = majorMinorPatch.getAt(1).toInteger()
    if (minor > 99) {
        throw new GradleException("Minor version of application $minor can not be higher than 99")
    }

    int patch = majorMinorPatch.getAt(2).toInteger()
    if (patch > 9) {
        throw new GradleException("Patch (hotfix) version $patch can not be higher than 9")
    }

    return major * 1000 + minor * 10 + patch
}