apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    archiveClassifier.convention('sources')
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokka) {
    archiveClassifier.set('javadoc')
    archiveClassifier.convention('javadoc')
    from dokka.outputDirectory
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId = project.groupId
            artifactId = project.artifactId
            version = project.libVersion

            afterEvaluate {
                artifact bundleReleaseAar
                artifact sourcesJar
                artifact javadocJar
            }

            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}