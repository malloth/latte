apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace "$groupId.$artifactId".replace('-', '_')
    compileSdk Integer.parseInt(COMPILE_SDK)

    defaultConfig {
        minSdkVersion Integer.parseInt(MIN_SDK)
        targetSdkVersion Integer.parseInt(TARGET_SDK)
        versionName libVersion
        versionCode libVersionCode
        archivesBaseName = "latte-$artifactId-v$libVersion"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'consumer-proguard-rules.pro'
        }
    }

    sourceSets.configureEach {
        it.java.srcDirs += "src/${it.name}/kotlin"
    }

    lint {
        abortOnError true
        warningsAsErrors true
        textReport false
        htmlReport false
        xmlOutput file("$rootDir/build/reports/lint/${project.name}.xml")
        lintConfig file('lint.xml')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        allWarningsAsErrors = true
        moduleName = "$groupId.$artifactId"
        freeCompilerArgs += ['-Xexplicit-api=strict']
        jvmTarget = "17"
    }

    testOptions {
        animationsDisabled true

        unitTests {
            returnDefaultValues = true
            all {
                useJUnitPlatform()
                reports {
                    junitXml {
                        destination = file("$rootDir/build/test-results/${project.name}")
                    }
                }
            }
        }
    }

    packagingOptions {
        resources {
            merges += ['META-INF/LICENSE.md', 'META-INF/LICENSE-notice.md']
        }
    }
}